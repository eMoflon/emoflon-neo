import "platform:/resource/SokobanLanguage/src/metamodels/SokobanLanguage.msl"

tripleGrammar SokobanImportExport {
	source {
		sokobanExchangeFormat
	}
	
	target {
		SokobanLanguage
	}
	
	correspondence {
		Entry <-EntryToField-> Field
	
		sokobanExchangeFormat.Board <-BoardToBoard-> SokobanLanguage.Board
	}
	
	rules {
		BoardEndEntryRule
		BoardNormalEntryRule
		
		FirstColAllRowsEnd
		FirstColAllRowsNormal

		FirstRowAllColsEnd
		FirstRowAllColsNormal

		AllOtherFieldsEnd
		AllOtherFieldsNormal		
	}
}


abstract tripleRule BoardToBoardRule : SokobanImportExport {
	source {
		++ sokBoard: Board {
			++ -firstRow-> row
		}
		++ row: Row {
			++ -firstEntry->ne
		}
		++ ne: Entry
	}

	target {
		++ board: Board {
			++ -fields->nf {
				.row := 0
				.col := 0
			}
		}
		++ nf: Field
	}
	
	correspondence {
		++ sokBoard <-:BoardToBoard-> board
		 
		++ ne <-:EntryToField-> nf
	}
}

tripleRule BoardEndEntryRule:SokobanImportExport -> BoardToBoardRule, EndRule

tripleRule BoardNormalEntryRule:SokobanImportExport -> BoardToBoardRule, NormalRule



abstract tripleRule EndRule:SokobanImportExport {
	source {
		++ ne: End
	}
	
	target {
		++ nf: Field {
			.endPos := true
		}
	}
}

abstract tripleRule NormalRule:SokobanImportExport {
	source {
		++ ne: Normal
	}
	
	target {
		++ nf: Field {
			.endPos := false
		}
	}
}

abstract tripleRule AllOtherFields:SokobanImportExport {
	source {
		ble: Entry {
			++ -next->ne
		}
		
		++ ne: Entry
	}
	
	target {
		b: Board {
			-fields->ul
			-fields->bl
			-fields->ur
			++ -fields->nf {
				.row := b::-fields->bl::row
				.col := b::-fields->ur::col
			}
		}
		
		ul: Field {
			-right->ur
			-bottom->bl
		}
		
		ur: Field {
			++ -bottom->nf
		}
		
		bl: Field {
			++ -right->nf
		}
		
		++ nf: Field
	}
	
	correspondence {
		ble <-:EntryToField-> bl
		
		++ ne <-:EntryToField-> nf
	}
}

tripleRule AllOtherFieldsEnd:SokobanImportExport -> AllOtherFields, EndRule
forbid trg(NoDoubleBottomToo) && trg(NoDoubleRightToo)

pattern NoDoubleBottomToo -> NoDoubleBottom[f->ur]
pattern NoDoubleRightToo -> NoDoubleRight[f->bl]

tripleRule AllOtherFieldsNormal:SokobanImportExport -> AllOtherFields, NormalRule
forbid trg(NoDoubleBottomToo) && trg(NoDoubleRightToo)

abstract tripleRule FirstRowAllCols:SokobanImportExport -> FirstFields {
	source {
		e:Entry {
			++ -next->$ne
		}
	}
	
	target {
		b:Board {
			-fields->f {				
				.row : 0
			}
			++ -fields->$nf {
				.row := 0
				.col := b::-fields->f::col + 1
			}
		}
		
		f: Field {
			++ -right->$nf
		}
	}
}

tripleRule FirstRowAllColsEnd:SokobanImportExport -> FirstRowAllCols, EndRule
forbid trg(NoDoubleRight)

pattern NoDoubleRight {
	f:Field {
		-right->other
	}
	
	other:Field
}

tripleRule FirstRowAllColsNormal:SokobanImportExport -> FirstRowAllCols, NormalRule
forbid trg(NoDoubleRight)

abstract tripleRule FirstFields:SokobanImportExport {
	source {
		e: Entry
		
		++ ne: Entry
	}
	
	target {
		b: Board {
			-fields->f
			++ -fields->nf
		}
		
		f: Field
		
		++ nf: Field
	}
	
	correspondence {
		e <-:EntryToField-> f
		
		++ ne <-:EntryToField-> nf
	}
}

abstract tripleRule FirstColAllRows:SokobanImportExport -> FirstFields {
	source {
		r: Row {
			-firstEntry-> $e 
			++ -next-> nr
		}
		
		++ nr: Row {
			++ -firstEntry-> $ne
		}
	}
	target {
		b:Board {
			-fields->f {				
				.col : 0
			}
			++ -fields->nf {
				.col := 0
				.row := b::-fields->f::row + 1
			}
		}
		
		f: Field {
			++ -bottom-> nf
		}
		
		++ nf: Field
	}
}

tripleRule FirstColAllRowsEnd:SokobanImportExport -> FirstColAllRows, EndRule
forbid trg(NoDoubleBottom)

tripleRule FirstColAllRowsNormal:SokobanImportExport -> FirstColAllRows, NormalRule
forbid trg(NoDoubleBottom)

pattern NoDoubleBottom {
	f:Field {
		-bottom->other
	}
	
	other:Field
}