import "platform:/resource/FacebookToInstagram/src/Facebook.msl"
import "platform:/resource/FacebookToInstagram/src/Instagram.msl"

tripleGrammar FacebookToInstagramGrammar {
	source {
		FacebookLanguage
	}
  	
	target {
		InstagramLanguage
	}
	
	correspondence {
		FacebookLanguage.Network <-NetworkToNetwork-> InstagramLanguage.Network
		
		FacebookLanguage.User <-FbUserToInUser-> InstagramLanguage.User
	}
	
	rules {
		// Axioms
		NetworkToNetworkIslandRule
		UserToUserIslandRule
		
		// Connect users to networks
		UserNetworkBridgeRule
		
		// Handle friendship/followership
		RequestFriendship
		AcceptFriendship
		IgnoreInterNetworkFollowers
		
		// Ignore all extra relationships
		ParentsIgnoreRule
		SpouseIgnoreRule
		SiblingsIgnoreRule		
	}
}

abstract tripleRule Followers : FacebookToInstagramGrammar {
	source {
		n : Network {
			-users-> fn
		}
		
		fu : User
		fn : User
	}
	
	target {
		iu : User 
		
		in : User 
	}
	
	correspondence {
		fu <-:FbUserToInUser-> iu
		
		fn <-:FbUserToInUser-> in
	}
}

tripleRule AcceptFriendship : FacebookToInstagramGrammar -> Followers {
	source {
		n : Network {
			-users-> $fu
			++ -friendships-> fri
		}
		
		++ fri : Friendship {
			++ -friends-> $fu
			++ -friends-> $fn
		}
	}
	
	target {
		in : User {
			++ -follows->iu
		}
		
		iu : User {
			-follows->in
		}
	}
} forbid src(NoDoubleFriendshipFacebook) && trg(NoDoubleFollowershipInstagram)

pattern NoDoubleFriendshipFacebook {
	n:FacebookLanguage.Network {
		-friendships->other
	}
	
	fn : FacebookLanguage.User
	
	fu : FacebookLanguage.User
	
	other : Friendship {
		-friends-> fn
		-friends-> fu
	}
}

pattern NoDoubleFollowershipInstagram {
	in : InstagramLanguage.User {
		-follows-> iu
	}
	
	iu : InstagramLanguage.User
}

pattern NoRedundantFollowershipInstagram -> NoDoubleFollowershipInstagram[in -> iu, iu -> in]

abstract tripleRule UserRelationIgnoreRule : FacebookToInstagramGrammar {
	source {
		fu : User
		
		fn : User
	}
}

tripleRule ParentsIgnoreRule : FacebookToInstagramGrammar -> UserRelationIgnoreRule {
	source {
		fu : User {
			++ -parents-> $fn
		}
	}
} forbid src(NoDoubleParent) && src(NoCyclicParenthood)

pattern NoDoubleParent {
	fn:FacebookLanguage.User
	
	fu : FacebookLanguage.User {
		-parents-> fn
	}
}

pattern NoCyclicParenthood -> NoDoubleParent[fn -> fu, fu -> fn]

tripleRule SpouseIgnoreRule : FacebookToInstagramGrammar {
	source {
		fu : User {
			++ -spouse-> fn
		}
		
		fn : User {
			++ -spouse-> fu
		}
	}
} forbid src(NoDoubleSpouse)

pattern NoDoubleSpouse {
	fn:FacebookLanguage.User {
		-spouse-> fu
	}
	
	fu : FacebookLanguage.User {
		-spouse-> fn
	}
}

tripleRule SiblingsIgnoreRule : FacebookToInstagramGrammar {
	source {
		fn : User {
			++ -siblings-> fu
		}
		
		fu : User {
			++ -siblings-> fn
		}
	}
} forbid src(NoDoubleSibling)

pattern NoDoubleSibling {
	fn:FacebookLanguage.User {
		-siblings->fu
	}
	
	fu : FacebookLanguage.User {
		-siblings-> fn
	}
}

tripleRule IgnoreInterNetworkFollowers : FacebookToInstagramGrammar -> Followers {
	source {
		nu : Network {
			-users-> $fu
		}
	}
	
	target {	
		in : User {
			++ -follows-> iu
		}
		
		iu : User {
			-follows-> in
		}
	}
} forbid trg(NoDoubleFollowershipInstagram)

tripleRule RequestFriendship : FacebookToInstagramGrammar {
	target {
		iu : User
		
		in : User {
			++ -follows-> iu
		}
	}
} forbid trg(NoDoubleFollowershipInstagram) && trg(NoRedundantFollowershipInstagram)

tripleRule NetworkToNetworkIslandRule : FacebookToInstagramGrammar {
	source {
		++ fl : Network {
			.description := <networkDesc>
		}
	}
	
	target {
		++ il : Network {
			.description := <networkDesc>
		}
	}
	
	correspondence {
		++ fl <-:NetworkToNetwork-> il
	}
}

tripleRule UserNetworkBridgeRule : FacebookToInstagramGrammar {
	source {
		f : Network {
			++ -users-> fu
		}
		
		fu : User
	}
	
	target {
		i : Network {
			++ -users-> iu
		}
		
		iu : User
	}
	
	correspondence {
		f <-:NetworkToNetwork-> i
		fu <-:FbUserToInUser-> iu
	}
}  forbid src(AlreadyInFBNetwork) && src(AlreadyInAFBNetwork) && trg(AlreadyInInstNetwork) && trg(AlreadyInAnInstNetwork)

pattern AlreadyInAFBNetwork -> AlreadyInFBNetwork[f -> other]

pattern AlreadyInFBNetwork {
	f:FacebookLanguage.Network {
		-users->fu
	}
	
	fu:FacebookLanguage.User
}

pattern AlreadyInAnInstNetwork -> AlreadyInInstNetwork[i -> other]

pattern AlreadyInInstNetwork {
	i:InstagramLanguage.Network {
		-users->iu
	}
	
	iu:InstagramLanguage.User
}

tripleRule UserToUserIslandRule : FacebookToInstagramGrammar {
	source {
		++ flu : User {
			.name := <userName>
		}
	}
	
	target {
		++ ilu : User {
			.name := <userName>
		}
	}
	
	correspondence {
		++ flu <-:FbUserToInUser-> ilu
	}
}









